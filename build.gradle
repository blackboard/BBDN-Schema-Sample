apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"

// define the version for the project when publishing to maven
group "blackboard.plugin"
version getB2Version()

project.ext {
  learnVersion = "9.1.201410.160373"
  springVersion = "3.1.1.RELEASE"
  hibernateVersion = "4.1.10.Final"
  deployServer = "localhost:9876"
  deployInstance = "BBLEARN"
}

String getB2Version() {
  File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
  def manifest = new XmlSlurper().parse(mfFile);
  return manifest.plugin.version['@value'];
}

repositories {
  mavenCentral()
  maven {
    url "https://maven.blackboard.com/content/repositories/releases/"
  }
}

configurations {
  buildUtils
}

// define the project's dependencies
dependencies {
  // providedCompile dependencies are libraries needed to build, but should not be included in the B2 WAR.
  providedCompile( "blackboard.platform:bb-platform:$ext.learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-cms-admin:$ext.learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-taglibs:$ext.learnVersion" ) { transitive = false }

  compile( "blackboard.platform:bb-spring-webapi:10.1" ) { transitive = false }

  compile "org.springframework:spring-aop:$ext.springVersion",
          "org.springframework:spring-asm:$ext.springVersion",
          "org.springframework:spring-beans:$ext.springVersion",
          "org.springframework:spring-context:$ext.springVersion",
          "org.springframework:spring-core:$ext.springVersion",
          "org.springframework:spring-expression:$ext.springVersion",
          "org.springframework:spring-web:$ext.springVersion",
          "org.springframework:spring-webmvc:$ext.springVersion",
          "org.springframework:spring-context-support:$ext.springVersion",
          "org.springframework:spring-jdbc:$ext.springVersion",
          "org.springframework:spring-orm:$ext.springVersion",
          "org.springframework:spring-tx:$ext.springVersion",
          "javax.servlet:servlet-api:2.4",
          "commons-lang:commons-lang:2.6",
          "com.googlecode.json-simple:json-simple:1.1",
          "org.codehaus.jackson:jackson-mapper-asl:1.8.5",
          "commons-logging:commons-logging:1.1.1",
          "junit:junit:4.5",
          "org.json:json:20090211",
          "com.google.guava:guava:18.0"      

  compile( "org.hibernate:hibernate-core:$ext.hibernateVersion" ) { transitive = true }
  compile "org.hibernate:hibernate-entitymanager:$ext.hibernateVersion"

  runtime "org.javassist:javassist:3.17.1-GA",
          "dom4j:dom4j:1.6.1"

  buildUtils "org.oscelot:b2deploy-task:0.1.0"
}


// Add a task to deploy a B2 using starting block
task deployB2( dependsOn: "war" ) << {
      ant.taskdef( name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath )
      ant.b2deploy( localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance )
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.6'
}
